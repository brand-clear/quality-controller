version "5.0"
#===================================================
# Probe cylindrical features and send to formatted 
# report.
#
# Parameters
# ----------
# $1 : Cylinder type
#     {"Body", "Hole"}
#---------------------------------------------------


DECLARE finished 0
DECLARE previousCount
DECLARE currentCount
DECLARE macroPath "Q:\Quality Control\quality_controller\macros"


FEATURE PRIMITIVE OPTIONS PROBE MANUAL EXTRACT_NOMINAL ( "On" )

WHILE $finished == 0
    
    TREEVIEW FEATURE VIEW HIDE ALL
    TREEVIEW FEATURE COUNT GET ( previousCount )
    FEATURE PRIMITIVE CYLINDER PROBE ( "Measured" )
    TREEVIEW FEATURE COUNT GET ( currentCount )
    
    # This condition indicates that a new dimension was not captured
    IF $previousCount == $currentCount
        SET finished 1
        CONTINUE
    ENDIF
    
    
    # Get appropriate metrics
    IF $1 == "Body"
        MACRO EXEC ( "${macroPath}\controls-cylinder-body.pwmacro" )
        
    ELSEIF $1 == "Hole"
        MACRO EXEC ( "${macroPath}\controls-cylinder-hole.pwmacro" )
        
    ENDIF
    
    # Send data to report
    MACRO EXEC ( "${macroPath}\capture-snapshot.pwmacro" )
    
ENDWHILE