version "5.0"
#=====================================================
# This script prompts the user to probe a planar  
# datum feature (keyway) per a given input.
#
# Parameters
# ----------
# $1 : Keyway feature name
#-----------------------------------------------------


DECLARE name "$1"
DECLARE measured
DECLARE tolerance
DECLARE formControl
DECLARE profileControl
DECLARE tryAgain 1
DECLARE errorStatus
DECLARE macroPath "Q:\Quality Control\quality_controller\macros"


MACRO GUIDING_INSTRUCTIONS ( "On", "Probe ${name}", , , "off")

WHILE $tryAgain == 1
    
    # Probe plane
    FEATURE PRIMITIVE PLANE PROBE ( "Measured", $name )
    
    # Flatness test
    SET measured "Flatness"
    SET tolerance 0.001
    MACRO EXEC ( "${macroPath}\metric-sansdatum.pwmacro", measured, tolerance, formControl )
    IF $measured > $tolerance
        MACRO INPUT QUESTION ( tryAgain, {"The measured flatness exceeds the datum tolerance of ${tolerance}.", "Do you want to try again?"} )
        IF $tryAgain == 1
            MEASURE CONTROL REMOVE ( $formControl )
            CONTINUE
        ENDIF
    ELSE
        SET tryAgain 0
    ENDIF
    
    # Get profile
    SET measured "Surface Profile"
    SET tolerance 0.004
    MACRO EXEC ( "${macroPath}\metric-sansdatum.pwmacro", measured, tolerance, profileControl )
    
    TREEVIEW OBJECT VIEW RESTORE ( $name )
    # If no plane was probed an error will be raised
    # when trying to restore the feature.
    MACRO GET_ERROR_STATUS ( errorStatus )
    IF $errorStatus == "Error"
        MACRO END ( "Error" )
    ENDIF
    
    MACRO EXEC ( "${macroPath}\capture-snapshot.pwmacro" )
    
ENDWHILE


# Prep for next operation
MACRO EXEC ( "${macroPath}\process-conclude.pwmacro" )